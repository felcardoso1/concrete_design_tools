! 
!Dimensionamento à Flexo-Compressão Normal 
!Seções retangulares com várias camadas de armadura 
!O programa está limitado a 20 camadas 
! 
!PROGRAMA PRINCIPAL 
! 
!Declarando variáveis reais de dupla precisão 
IMPLICIT REAL*8(A-H,O-Z) 
!Variáveis públicas 
COMMON /A1/ ni(20),beta(20) 
COMMON /A2/ n,nl 
COMMON /A3/ delta,ani,ami,fyd,fcd,es 
COMMON /A4/ alfac,alamb,e0,eu,akapa 
! 
! 
!Abertura do arquivo de dados 
OPEN(1,FILE='DADOS.TXT') 
! 
!LEITURA DOS DADOS 
! 
!Os dados são lidos do arquivo DADOS.TXT 
!Cada dado deve estar em uma linha do arquivo 
!Resistência característica à compressão do concreto em MPa 
read(1,*)fck 
! 
!Tensão de escoamento característica do aço em MPa 
read(1,*)fyk 
! 
!Módulo de elasticidade do aço em GPa 
read(1,*)es 
! 
!Coeficientes parciais de segurança: 
!para o concreto 
read(1,*)gamac 
!para o aço 
read(1,*)gamas 
!para os esforços solicitantes 
read(1,*)gamaf 
! 
!Dados da seção transvesral: 
!Largura em cm 
read(1,*)b 
!Altura em cm 
read(1,*)h 
!Distância d! 
read(1,*)dl 
! 
!Número de camadas de armadura: 
!A seção deve ter, no mínimo, duas camadas de armadura 
!nl>=2 
read(1,*)nl 
! 
!Número de barras das camadas de armadura: 
!As camadas são numeradas de baixo para cima 
!Cada camada deve ter, no mínimo, duas barras 
do i=1,nl 
 read(1,*)ni(i) 
end do 
!Esforços solicitantes de serviço: 
!Entrar com valores positivos 
!Esforço normal (em kN) 
read(1,*)ank 
!Momento fletor (em kNm) 
read(1,*)amk 
! 
! 
!FIM DA ENTRADA DE DADOS 
! 
!INÍCIO DOS CÁLCULOS 
! 
!Parâmetros do diagrama retangular 
If(fck<=50)Then 
 alamb=0.8 
 alfac=0.85 
 eu=3.5 
 e0=2 
Else 
 alamb=0.8-(fck-50)/400 
 alfac=0.85*(1-(fck-50)/200) 
 eu=2.6+35*((90-fck)/100)**4 
 e0=2+0.085*((fck-50)**0.53) 
End If 
!Parâmetro kapa que define o ponto com deformação igual a eo no domínio 5 
akapa=1-e0/eu 
!
!Conversão de unidades: transformando para kN e cm 
fck=fck/10 
fyk=fyk/10 
es=100*es 
amk=100*amk 
! 
!Resistências de cálculo 
fcd=fck/gamac 
tcd=alfac*fcd 
fyd=fyk/gamas 
! 
!Esforços solicitantes de cálculo 
aand=gamaf*ank 
amd=gamaf*amk 
! 
!Cálculo do número total de barras na seção 
n=0 
do i=1,nl 
 n=n+ni(i) 
end do 
! 
!Parâmetro geométrico 
delta=dl/h 
! 
!Área da seção de concreto 
ac=b*h 
! 
!Esforcos reduzidos 
ani=aand/(ac*tcd) 
ami=amd/(ac*h*tcd) 
! 
!Caso particular de compressão centrada 
! 
If(ami.Eq.0)Then 
 esi=e0/1000 
 Call Tensao(esi,tsd0) 
 w=(ani-1)*fyd/tsd0 
 If(w<0)Then 
 w=0 
 End If 
 GoTo 100 
End If 
! 
!Flexo-compressão normal 
! 
!Montagem do vetor beta 
!Ver equação (2.2.5) do Volume 3 de Curso de Concreto Armado 
! 
do i=1,nl 
 beta(i)=delta+(nl-i)*(1-2*delta)/(nl-1) 
end do 
!Processo iterativo da bissecante 
! 
!Determinação do intervalo solução 
! 
!Valor inicial para a linha neutra adimensional qsi=x/h 
qi=0
!Chamar sub-rotina para calcular o valor da função fi=f(qi) 
Call Funcao(qi,rc,bc,soma1,soma2,fi) 
! 
!Valor final para a linha neutra adimensional qsi=x/h 
qf=1000 
!Chamar sub-rotina para calcular o valor da função ff=f(qf) 
Call Funcao(qf,rc,bc,soma1,soma2,ff) 
! 
prod=fi*ff 
!Modificando os extremos do intervalo solução até que prod<=0 
Do While (prod>0) 
 qi=qf 
 fi=ff 
 qf=10*qf 
 Call Funcao(qf,rc,bc,soma1,soma2,ff) 
 prod=fi*ff 
End do 
! 
!O intervalo solução foi definido 
!A linha neutra qsi fica entre [qi,qf] 
! 
!Processo iterativo da bissecante 
fk=1 
Do While (Abs(fk)>0.001) 
 qk=(qi*ff-qf*fi)/(ff-fi) 
 Call Funcao(qk,rc,bc,soma1,soma2,fk) 
 prod=fk*fi 
 If(prod>=0)Then 
 qi=qk 
 fi=fk 
 Else 
 qf=qk 
 ff=fk 
 End If 
End do 
!Convergência alcançada 
!qk é a raiz da função f(qsi) dada na equação (2.5.11) do Volume 3 de Curso de 
!Concreto Armado 
! 
!Cálculo da taxa mecânica de armadura 
If(Abs(soma1)>=Abs(soma2))Then 
! Uso da equação (2.5.9) do Volume 3 
 w=n*fyd*(ani-rc)/soma1 
Else 
! Uso da equação (2.5.10) do Volume 3 
 w=n*fyd*(0.5*ani-ami-rc*bc)/soma2 
End If 
If(w<0)Then 
 w=0 
End If 
! 
!Cálculo da área de armadura total 
100 aas=w*ac*tcd/fyd 
! 
!Armadura mínima da NBR-6118 para pilares 
! 
ani0=aand/(ac*fcd)

romin=0.15*fcd*ani0/fyd 
If(romin<0.004)Then 
 romin=0.004 
End If 
asmin=romin*ac 
! 
!Armadura a ser adotada 
ase=aas 
If(ase<asmin) Then 
 ase=asmin 
End If 
! 
!MOSTRAR O RESULTADO 
! 
write(*,10)aas 
10 format(//,5x,'Armadura calculada: As(cm2)=',f6.2) 
write(*,20)asmin 
20 format(//,5x,'Armadura minima: As,min(cm2)=',f6.2) 
write(*,30)ase 
30 format(//,5x,'Armadura a ser adotada: Ase(cm2)=',f6.2) 
! 
End 
! 
! 
! 
Subroutine Funcao(qsi,rc,bc,soma1,soma2,f) 
IMPLICIT REAL*8(A-H,O-Z) 
!Variáveis públicas 
COMMON /A1/ ni(20),beta(20) 
COMMON /A2/ n,nl 
COMMON /A3/ delta,ani,ami,fyd,fcd,es 
COMMON /A4/ alfac,alamb,e0,eu,akapa 
! 
!Calcula o valor da função f(qsi) dada na equação (2.5.11) do Volume 3 de Curso de 
!Concreto Armado 
! 
!qsi=x/h é a profundidade relativa da linha neutra 
!rc é a resultante de compressão do concreto adimensional dada na equação (2.4.4) 
!bc é a posição da resultante adimensional dada na equação (2.4.5) 
!soma1 é o somatório contido no denominador da equação (2.5.9) 
!soma2 é o somatatório contido no denominador da equação (2.5.10) 
!f é o resultado da equação (2.5.11) 
! 
!O único parâmetro de entrada é qsi, pois as demais variáveis são públicas. 
!Os parâmetros rc,bc,soma1,soma2,f são calculados nessa sub-rotina 
! 
! 
!Constantes para o cálculo das deformações das camadas da armadura 
ql=eu*beta(1)/(eu+10) 
If(qsi<=ql)Then 
 !A linha neutra está no domínio 2 
 c=0.01/(beta(1)-qsi) 
ElseIf(qsi<=1)Then 
 !A linha neutra está nos domínios 3,4 e 4a 
 c=eu/(1000*qsi) 
Else 
 !A linha neutra está no domínio 5
c=(e0/1000)/(qsi-akapa) 
End If 
! 
!Resultante de compressão no concreto 
If(qsi<1/alamb)Then 
 rc=alamb*qsi 
 bc=0.5*alamb*qsi 
Else 
 rc=1 
 bc=0.5 
End If 
soma1=0 
soma2=0 
do i=1,nl 
 esi=c*(qsi-beta(i)) 
 Call Tensao(esi,tsi) 
 soma1=soma1+ni(i)*tsi 
 soma2=soma2+ni(i)*beta(i)*tsi 
end do 
!Função f(qsi) 
f=(ami-0.5*ani+rc*bc)*soma1+(ani-rc)*soma2 
Return 
End 
! 
! 
! 
Subroutine Tensao(esl,tsl) 
IMPLICIT REAL*8(A-H,O-Z) 
!Variáveis públicas 
COMMON /A3/ delta,ani,ami,fyd,fcd,es 
! 
!Calcula a tensão no aço 
!es = módulo de elasticidade do aço em kN/cm2 
!esl = deformação de entrada 
!fyd = tensão de escoamento de cálculo em kN/cm2 
!tsl = tensão de saída em kN/cm2 
! 
!Trabalhando com deformação positiva 
ess=Abs(esl) 
eyd=fyd/es 
If(ess<eyd)Then 
 tsl=es*ess 
Else 
 tsl=fyd 
End If 
!Trocando o sinal se necessário 
If(esl<0)Then 
 tsl=-tsl 
End If 
Return 
End   