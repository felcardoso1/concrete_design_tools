! 
!Dimensionamento de seções retangulares à Torção combinada com Flexão e Esforço Cortante 
! 
! 
!PROGRAMA PRINCIPAL 
!
!Declarando variáveis reais de dupla precisão 
IMPLICIT REAL*8(A-H,O-Z) 
! 
!Abertura do arquivo de dados 
OPEN(1,FILE='DADOS.TXT') 
! 
!LEITURA DOS DADOS 
! 
!Os dados são lidos do arquivo DADOS.TXT 
!Cada dado deve estar em uma linha do arquivo 
! 
! 
! 
!Resistência característica à compressão do concreto em MPa 
read(1,*)fck 
! 
!Tensão de escoamento característica do aço em MPa 
read(1,*)fyk 
! 
!Módulo de elasticidade do aço em GPa 
read(1,*)es 
! 
!Coeficientes parciais de segurança: 
!para o concreto 
read(1,*)gamac 
! 
!para o aço 
read(1,*)gamas 
! 
!para o momento fletor 
read(1,*)gamaf 
! 
!Coeficiente beta de redistribuição de momentos 
read(1,*)bduct 
! 
!Largura da seção transversal 
read(1,*)b 
! 
!Altura da seção transversal 
read(1,*)h 
! 
!Parâmetro d! 
read(1,*)dl 
! 
!Momento fletor de serviço em kNm 
read(1,*)amk 
! 
!Esforço cortante em kN 
read(1,*)vk 
! 
!Momento torçor em kNm 
read(1,*)tk 
! 
!FIM DA ENTRADA DE DADOS 
! 
!INÍCIO DOS CÁLCULOS 
!
!Parâmetros do diagrama retangular 
If (fck<=50) Then 
 alamb=0.8 
 alfac=0.85 
 eu=3.5 
 qlim=0.8*bduct-0.35 
Else 
 alamb=0.8-(fck-50)/400 
 alfac=0.85*(1-(fck-50)/200) 
 eu=2.6+35*((90-fck)/100)**4 
 qlim=0.8*bduct-0.45 
End If 
! 
! 
!Conversão de unidades: transformando para kN e cm 
amk=100*amk 
tk=100*tk 
es=100*es 
! 
!Resistências de cálculo em kN/cm2 
! 
fcd=fck/(10*gamac) 
tcd=alfac*fcd 
fyd=fyk/(10*gamas) 
! 
!Esforços solicitantes de cálculo 
! 
amd=gamaf*amk 
vd=gamaf*vk 
td=gamaf*tk 
! 
!Altura útil 
d=h-dl 
! 
!Parâmetro geométrico 
delta=dl/d 
! 
! 
!Dimensionamento à Flexão Simples 
! 
! 
!Momento limite 
amilim=alamb*qlim*(1-0.5*alamb*qlim) 
! 
!Momento reduzido solicitante 
ami=amd/(b*d*d*tcd) 
! 
If (ami<=amilim) Then 
!Armadura simples 
 qsi=(1- Sqrt(1-2*ami))/alamb 
 aas=alamb*qsi*b*d*tcd/fyd 
 asl=0 
Else 
!Armadura dupla 
! 
! Evitando armadura dupla no domínio 2 
 qsia=eu/(eu+10)
If (qlim<qsia) Then 
! 
! Está resultando armadura dupla no domínio 2. 
! Colocar mensagem para o usuário aumentar as dimensões da seção transversal e 
! parar o processamento 
! 
 write(*,10) 
10 format(//,5x,'Resultou armadura dupla no domínio 2. Aumente as dimensoes da secao transversal.') 
 Goto 200 
 End If 
! 
! Eliminando o caso em que qlim<delta 
! Se isto ocorrer, a armadura de compressão estará tracionada 
! 
 If (qlim<=delta) Then 
! 
! Colocar mensagem para o usuário aumentar as dimensões da seção transversal e 
! parar o processamento 
! 
write(*,20) 
20 format(//,5x,'Aumente as dimensoes da secao transversal.') 
 Goto 200 
 End If 
! 
! Deformação da armadura de compressão 
 esl=eu*(qlim-delta)/qlim 
 esl=esl/1000 
! Tensão na armadura de compressão 
! Chamar sub-rotina 
 Call Tensao(es,esl,fyd,tsl) 
 asl=(ami-amilim)*b*d*tcd/((1-delta)*tsl) 
 aas=(alamb*qlim+(ami-amilim)/(1-delta))*b*d*tcd/fyd 
End If 
! 
!Armadura mínima 
a=2./3. 
fyd=10*fyd 
If (fck<=50) Then 
 romin=0.078*(fck**a)/fyd 
Else 
 romin=0.5512*Log(1+0.11*fck)/fyd 
End If 
If (romin<0.0015) Then 
 romin=0.0015 
End If 
! 
asmin=romin*b*h 
If (aas<asmin) Then 
 aas=asmin 
End If 
! 
! 
!Dimensionamento ao Esforço Cortante e Torção 
! 
! 
!Verificação do esmagamento das bielas considerando a superposição das tensões 
!
!Parcela devida ao esforço cortante 
! 
bw=b 
!Tensão convencional de cisalhamento 
twd=vd/(bw*d) 
!Passando para MPa 
twd=10*twd 
! 
!Tensão de cisalhamento última 
fcd=10*fcd 
av=1-fck/250 
twu=0.27*av*fcd 
! 
!Parcela devida ao momento torçor 
! 
!Parâmetros da seção vazada equivalente 
!Ver Capítulo 1 do Volume 4 de Curso de Concreto Armado 
!Com referência às Figuras 1.2.1 e 1.2.2 
!t0=espessura da parede da seção vazada 
!ae=área limitada pela linha média 
!up=perímetro da linha média 
! 
c1=dl 
t0=b*h/(2*(b+h)) 
If (t0>=2*c1) Then 
! Caso 1: Fig.1.2.1 
 t=t0 
 ae=(b-t)*(h-t) 
 up=2*(b+h-2*t) 
Else 
! Caso 2: Fig.1.2.2 
 t=t0 
 tmax=b-2*c1 
 If (t>tmax) Then 
 t=tmax 
 End If 
 ae=(b-2*c1)*(h-2*c1) 
 up=2*(b+h-4*c1) 
End If 
!Tensão convencional de cisalhamento 
ttd=td/(2*ae*t) 
!Passando para MPa 
ttd=10*ttd 
!Tensão de cisalhamento última 
ttu=0.25*av*fcd 
! 
!Combinação das tensões e verificação 
soma=ttd/ttu+twd/twu 
If (soma> 1) Then 
write(*,30) 
30 format(//,5x,'Esmagamento da biela de compressão. Aumente as dimensoes da secao transversal.') 
Goto 200 
End If 
! 
! 
!Espaçamento máximo dos estribos 
!
If (soma<=0.67) Then 
 smax=0.6*d 
 If (smax>30) Then 
 smax=30 
 End If 
Else 
 smax=0.3*d 
 If (smax>20) Then 
 smax=20 
 End If 
End If 
! 
!Dimensionamento das armaduras para o esforço cortante 
! 
! 
!Tensão Talc de redução da armadura 
!Se gamac=1.4 irão resultar as expressões (7.4.6) e (7.4.7) do Volume 1 de Curso 
!de Concreto Armado 
! 
If (fck<=50) Then 
 a=2./3. 
 tc=0.126*(fck**a)/gamac 
Else 
 tc=0.8904*Log(1+0.11*fck)/gamac 
End If 
! 
!Tensão Tald para cálculo da armadura 
! 
tald=1.11*(twd-tc) 
If (tald<0) Then 
 tald=0 
End If 
! 
!Limitação da tensão de escoamento do aço conforme a NBR-6118 
! 
If(fyd>435) Then 
fyd=435 
End If 
! 
!Cálculo da armadura 
aswv=100*bw*tald/fyd 
! 
!Dimensionamento das armaduras para o momento torçor 
! 
fyd=fyd/10 
aswt=100*td/(2*ae*fyd) 
aslt=td*up/(2*ae*fyd) 
! 
! 
!Cálculo da armadura mínima: 
!A tensão fyk deve ser menor ou igual a 500 MPa 
fykmax=fyk 
If (fykmax>500) Then 
 fykmax=500 
End If 
! 
!Resistência média à tração do concreto 
! 
If (fck<=50) Then 
 a=2./3. 
 fctm=0.3*(fck**a) 
Else 
 fctm=2.12*Log(1+0.11*fck) 
End If 
! 
!Taxa mínima de armadura (ver equação (1.4.5) do Volume 4) 
rowmin=0.2*fctm/fykmax 
! 
!Superposição dos estribos 
asw=aswv+2*aswt 
aswmin=rowmin*100*bw 
If (asw<aswmin) Then 
 asw=aswmin 
End If 
! 
!Armadura longitudinal mínima da torção 
! 
aslmin=0.5*rowmin*up*bw 
If (aslt<aslmin) Then 
 aslt=aslmin 
End If 
! 
! 
!Mostrando os resultados na tela 
! 
write(*,40) 
40 format(//,5x,'Estribos de dois ramos:',/) 
write(*,50)aswv 
50 format(5x,'Para o cortante =',f6.2,2x,' cm2/m') 
write(*,60)aswt 
60 format(5x,'Para o torcor =',f6.2,2x,' cm2/m') 
write(*,70)asw 
70 format(5x,'Total =',f6.2,2x,' cm2/m') 
write(*,80)smax 
80 format(5x,'Espacamento maximo (cm) =',f6.1,//) 
write(*,90) 
90 format(5x,'Armadura longitudinal para o momento fletor:') 
write(*,100)aas,asl 
100 format(5x,'As =',f6.2,2x,' cm2',4x,'Aslinha =',f6.2,2x,' cm2') 
write(*,110)aslt 
110 format(/,5x,'Armadura longitudinal para o torcor =',f6.2,2x,' cm2') 
!Encerrando o programa 
200 a=0 
End 
! 
! 
! 
Subroutine Tensao(es, esl, fyd, tsl) 
IMPLICIT REAL*8(A-H,O-Z) 
! 
!Calcula a tensão no aço 
!es = módulo de elasticidade do aço em kN/cm2 
!esl = deformação de entrada 
!fyd = tensão de escoamento de cálculo em kN/cm2 
!tsl = tensão de saída em kN/cm2 
! 
!Trabalhando com deformação positiva 
ess = Abs(esl) 
eyd = fyd / es 
If (ess < eyd) Then 
 tsl = es * ess 
Else 
 tsl = fyd 
End If 
!Trocando o sinal se necessário 
If (esl < 0) Then 
 tsl = -tsl 
End If 
Return 
End
