!Flexão Simples Normal 
!Verificação de seções retangulares com várias camadas de armadura 
!O programa está limitado a 20 camadas 
! 
! 
!PROGRAMA PRINCIPAL 
! 
!Declarando variáveis reais de dupla precisão 
IMPLICIT REAL*8(A-H,O-Z) 
COMMON /ARMADURA/ asi(20),di(20),tens(20) 
! 
!Abertura do arquivo de dados 
OPEN(1,FILE='DADOS.TXT') 
! 
!LEITURA DOS DADOS 
! 
!Os dados são lidos do arquivo DADOS.TXT 
!Cada dado deve estar em uma linha do arquivo 
! 
!Resistência característica à compressão do concreto em MPa 
read(1,*)fck 
! 
!Tensão de escoamento característica do aço em MPa 
read(1,*)fyk 
!Módulo de elasticidade do aço em GPa 
read(1,*)es 
! 
!Coeficientes parciais de segurança: 
!para o concreto 
read(1,*)gamac 
! 
!para o aço 
read(1,*)gamas 
! 
!Largura da seção transversal 
read(1,*)b 
! 
!Número de camadas de armadura 
read(1,*)n 
! 
!Dados das camadas de armadura 
!As camadas são numeradas de baixo para cima
do i=1,n 
! Área de aço e altura útil 
 read(1,*)asi(i),di(i) 
end do 
! 
! 
!FIM DA ENTRADA DE DADOS 
! 
!INÍCIO DOS CÁLCULOS 
! 
!Parâmetros do diagrama retangular 
If(fck<=50)Then 
 alamb=0.8 
 alfac=0.85 
 eu=3.5 
Else 
 alamb=0.8-(fck-50)/400 
 alfac=0.85*(1-(fck-50)/200) 
 eu=2.6+35*((90-fck)/100)**4 
End If 
! 
!Conversão de unidades: transformando para kN e cm 
fck=fck/10 
fyk=fyk/10 
es=100*es 
! 
!Resistências de cálculo 
fcd=fck/gamac 
tcd=alfac*fcd 
fyd=fyk/gamas 
! 
!Cálculo do momento de ruína através do processo iterativo da bissecante 
! 
!Valor inicial para a linha neutra 
xi=0 
!Chamar sub-rotina 
Call funcao(b,xi,n,fcd,fyd,es,rc,alfac,alamb,eu,fi) 
! 
!Valor final para a linha neutra 
xf=di(1) 
!Chamar sub-rotina 
Call funcao(b,xf,n,fcd,fyd,es,rc,alfac,alamb,eu,ff) 
! 
!Processo iterativo da bissecante 
fk=1 
Do While (Abs(fk)>0.001) 
 xk=(xi*ff-xf*fi)/(ff-fi) 
 Call funcao(b,xk,n,fcd,fyd,es,rc,alfac,alamb,eu,fk) 
 prod=fk*fi 
 If(prod>0)Then 
 xi=xk 
 fi=fk 
 Else 
 xf=xk 
 ff=fk 
 End If 
End do 
!Convergência alcançada 
!xk é a raiz da função f(x) dada na equação (6.3.10) do Volume 1 de Curso de 
!Concreto Armado 
!Momento de ruina de cálculo 
x=xk 
zc=di(1)-0.5*alamb*x 
amu=rc*zc 
Do i=1,n 
 amu=amu+asi(i)*tens(i)*(di(1)-di(i)) 
End do 
!Passando o momento para kNm 
amu=amu/100 
! 
! 
!MOSTRAR O RESULTADO 
! 
write(*,10)amu 
10 format(//,5x,'Momento de ruina de calculo=',f6.2) 
!Fim do programa 
End 
! 
! 
! 
Subroutine Funcao(b,x,n,fcd,fyd,es,rc,alfac,alamb,eu,f) 
IMPLICIT REAL*8(A-H,O-Z) 
COMMON /ARMADURA/ asi(20),di(20),tens(20) 
! 
!Calcula o valor da função f(x) dada na equação (6.3.10) do Volume 1 de Curso de 
!Concreto Armado 
!O valor de saída é a variável f 
! 
tcd=alfac*fcd 
!Constantes para o cálculo das deformações das camadas de armadura 
xl=eu*di(1)/(eu+10) 
If(x<=xl)Then 
 !A linha neutra está no domínio 2 
 c=0.01/(di(1)- x) 
Else 
 !A linha neutra está nos domínios 3 ou 4 
 c=eu/(1000*x) 
End If 
!Resultante de compressão no concreto 
rc=alamb*b*x*tcd 
f=rc 
!Superpondo a contribuição das armaduras 
Do i=1,n 
 esi=c*(x-di(i)) 
 Call Tensao(es,esi,fyd,tsi) 
 tens(i)=tsi 
 f=f+asi(i)*tsi 
End do 
!Transformando f em adimensional para testar a convergência 
f=f/(b*di(1)*tcd) 
! 
Return 
End
! 
! 
! 
SUBROUTINE Tensao(es,esl,fyd,tsl) 
IMPLICIT REAL*8(A-H,O-Z) 
! 
!Calcula a tensao no aço 
!es = módulo de elasticidade do aço em kN/cm2 
!esl = deformação de entrada 
!fyd = tensão de escoamento de cálculo em kN/cm2 
!tsl = tensão de saída em kN/cm2 
! 
!Trabalhando com deformação positiva 
ess=Abs(esl) 
eyd=fyd/es 
If(ess<eyd)Then 
 tsl=es*ess 
Else 
 tsl=fyd 
End If 
!Trocando o sinal se necessário 
If(esl<0)Then 
 tsl=-tsl 
End If 
Return 
End